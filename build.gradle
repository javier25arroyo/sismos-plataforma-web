plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.10'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'cr.go.ice'
version = '0.0.1-SNAPSHOT'
description = 'Plataforma de monitoreo y visualización de datos acelerográficos'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	mockitoAgent
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.2'
	testImplementation 'org.testcontainers:mssqlserver:1.20.2'
	mockitoAgent('org.mockito:mockito-core') {
		transitive = false
	}
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	jvmArgs '-Xshare:off'
	jvmArgs "-javaagent:${configurations.mockitoAgent.singleFile}"
	// Exclude integration tests and context-loading tests that require external services in CI/local without DB/Docker
	exclude '**/*IntegrationTest*', '**/SismoPlatformApplicationTests*'
}
